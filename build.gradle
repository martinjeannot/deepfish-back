buildscript {
    ext {
        springBootVersion = '2.0.3.RELEASE'
        metaModelsSourcesDir = file("metamodels")
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id 'net.ltgt.apt' version '0.9'
    id 'io.franzbecker.gradle-lombok' version '1.11'
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'com.deepfish'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = JavaVersion.VERSION_1_8

configurations {
    querydslapt
}

sourceSets {
    main {
        java {
            srcDir metaModelsSourcesDir
        }
    }
}

bootRun {
    // support passing system properties via command-line (-D)
    systemProperties = System.properties
}

tasks.withType(JavaCompile) {
    // enable the standard JDK Reflection API to retrieve formal parameters names (used by Spring Security)
    options.compilerArgs << '-parameters'
}

task querymodels(type: JavaCompile, group: 'build') {
    doFirst {
        delete metaModelsSourcesDir
        metaModelsSourcesDir.mkdirs()
    }

    classpath = configurations.compile + configurations.querydslapt
    destinationDir = metaModelsSourcesDir

    source = sourceSets.main.java
    options.compilerArgs = [
            "-proc:only",
            /* we cannot use the com.querydsl.apt.jpa.JPAAnnotationProcessor anymore since our
            talent querying needs have complexified and we need some transient fields to be queryable (cf QueryableTalent) */
            "-processor", 'com.querydsl.apt.QuerydslAnnotationProcessor,lombok.launch.AnnotationProcessorHider$AnnotationProcessor',
            "-s", metaModelsSourcesDir
    ]
}

repositories {
    mavenLocal()
    mavenCentral()
}

ext {
    // update lombok when this is resolved : https://github.com/rzwitserloot/lombok/issues/1703
    lombokVersion = '1.16.22'
    mapstructVersion = '1.2.0.Final'
    pebbleVersion = '2.5.1'
    querydslVersion = '4.1.4'
    sendgridVersion = '4.1.2'
    simplejavamailVersion = '5.0.1'
    springCloudVersion = 'Finchley.RELEASE'
    vladmihalceaHibernateTypesVersion = '2.0.0'
}

dependencies {
    runtime("org.springframework.boot:spring-boot-properties-migrator")
    //compile('org.springframework.boot:spring-boot-starter-batch')
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.springframework.boot:spring-boot-starter-data-rest')
    //compile('org.springframework.boot:spring-boot-starter-mail')
    //compile('org.springframework.boot:spring-boot-starter-quartz')
    compile('org.springframework.boot:spring-boot-starter-security')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.flywaydb:flyway-core')
    compile('org.springframework.cloud:spring-cloud-starter-oauth2')
    runtime('org.postgresql:postgresql')
    compileOnly("org.projectlombok:lombok:${lombokVersion}")
    //testCompile('org.springframework.boot:spring-boot-starter-test')
    //testCompile('org.springframework.batch:spring-batch-test')
    //testCompile('org.springframework.security:spring-security-test')

    compile("org.simplejavamail:simple-java-mail:${simplejavamailVersion}")
    compile("com.sendgrid:sendgrid-java:${sendgridVersion}")
    compile('org.hibernate:hibernate-java8')
    compile("com.vladmihalcea:hibernate-types-5:${vladmihalceaHibernateTypesVersion}")
    compile("com.querydsl:querydsl-jpa:${querydslVersion}")
    querydslapt("com.querydsl:querydsl-apt:${querydslVersion}")
    compile("org.mapstruct:mapstruct-jdk8:${mapstructVersion}")
    apt("org.mapstruct:mapstruct-processor:${mapstructVersion}", "org.projectlombok:lombok")
    compile('com.fasterxml.jackson.datatype:jackson-datatype-jsr310')
    compile("io.pebbletemplates:pebble:${pebbleVersion}")
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}
